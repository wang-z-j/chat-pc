; å¯ç”¨ Unicode æ”¯æŒ
Unicode true

; å¼•å…¥å¸¸ç”¨æ¨¡å—
!include "MUI2.nsh"
!include "LogicLib.nsh"
!include "nsDialogs.nsh"

; åº”ç”¨ä¿¡æ¯
;!define PRODUCT_NAME "è“ç¿¼AIé—¨æˆ·"
;!define PRODUCT_VERSION "1.0.0"
!define PRODUCT_PUBLISHER "ccccltd, Inc."
!define PRODUCT_WEB_SITE "https://c4ai.ccccltd.cn/"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\lanyiAI.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

; å£°æ˜å˜é‡
Var Dialog
Var DesktopShortcutCheckbox
Var AutoStartCheckbox
Var TaskbarPinCheckbox
Var AutoStartState
Var TaskbarPinCheckState

; å®‰è£…å‘å¯¼é¡µé¢
!insertmacro MUI_PAGE_WELCOME
!define MUI_LICENSEPAGE_CHECKBOX
!insertmacro MUI_PAGE_LICENSE "${BUILD_RESOURCES_DIR}\license.txt"
!insertmacro MUI_PAGE_DIRECTORY
Page custom SetOptions SetOptionsLeave
!insertmacro MUI_PAGE_INSTFILES
!define MUI_FINISHPAGE_RUN "$INSTDIR\lanyiAI.exe"
!insertmacro MUI_PAGE_FINISH

; å¸è½½é¡µé¢
!insertmacro MUI_UNPAGE_INSTFILES

; è¯­è¨€
!insertmacro MUI_LANGUAGE "SimpChinese"

; å®‰è£…ç¨‹åºä¿¡æ¯
Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "lanyiAI_for_windows_${PRODUCT_VERSION}_setup.exe"
InstallDir "$PROGRAMFILES\ccccltd\lanyiAI"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

; è‡ªå®šä¹‰é€‰é¡¹é¡µé¢
Function SetOptions
  !insertmacro MUI_HEADER_TEXT "å®‰è£…é€‰é¡¹" "è¯·é€‰æ‹©éœ€è¦çš„å®‰è£…é€‰é¡¹"
  nsDialogs::Create 1018
  Pop $Dialog

  ${NSD_CreateCheckbox} 0 0 100% 12u "åˆ›å»ºæ¡Œé¢å¿«æ·æ–¹å¼"
  Pop $DesktopShortcutCheckbox
  ${NSD_SetState} $DesktopShortcutCheckbox ${BST_CHECKED}

  ${NSD_CreateCheckbox} 0 20u 100% 12u "å¼€æœºè‡ªåŠ¨å¯åŠ¨"
  Pop $AutoStartCheckbox
  ${NSD_SetState} $AutoStartCheckbox ${BST_CHECKED}

  ${NSD_CreateCheckbox} 0 40u 100% 12u "å›ºå®šåˆ°ä»»åŠ¡æ "
  Pop $TaskbarPinCheckbox
  ${NSD_SetState} $TaskbarPinCheckbox ${BST_CHECKED}

  nsDialogs::Show
FunctionEnd

Function SetOptionsLeave
  ${NSD_GetState} $DesktopShortcutCheckbox $0
  ${NSD_GetState} $AutoStartCheckbox $AutoStartState
  ${NSD_GetState} $TaskbarPinCheckbox $TaskbarPinCheckState
FunctionEnd

; å®‰è£…éƒ¨åˆ†
Section "Install"
  SetOutPath "$INSTDIR"
  DetailPrint "ğŸ’¡ APP_DIR = ${APP_DIR}"

  File /r "D:\a\chat-pc\chat-pc\dist\win-unpacked\*"


  ; åˆ›å»ºæ¡Œé¢å¿«æ·æ–¹å¼
  ${If} $0 == ${BST_CHECKED}
    CreateShortCut "$DESKTOP\${PRODUCT_NAME}.lnk" "$INSTDIR\lanyiAI.exe"
  ${EndIf}

  ; è®¾ç½®å¼€æœºè‡ªå¯
  ${If} $AutoStartState == ${BST_CHECKED}
    WriteRegStr HKCU "Software\Microsoft\Windows\CurrentVersion\Run" "${PRODUCT_NAME}" '"$INSTDIR\lanyiAI.exe"'
  ${EndIf}

  ; å›ºå®šåˆ°ä»»åŠ¡æ 
  ${If} $TaskbarPinCheckState == ${BST_CHECKED}
    ExecWait '"$INSTDIR\syspin.exe" "$INSTDIR\lanyiAI.exe" c:5386'
  ${EndIf}

  ; åˆ›å»ºå¼€å§‹èœå•é¡¹
  CreateDirectory "$SMPROGRAMS\${PRODUCT_NAME}"
  CreateShortCut "$SMPROGRAMS\${PRODUCT_NAME}\${PRODUCT_NAME}.lnk" "$INSTDIR\lanyiAI.exe"

  ; å†™å…¥å¸è½½å™¨
  WriteUninstaller "$INSTDIR\uninstall.exe"
SectionEnd

; é¢å¤–å¿«æ·æ–¹å¼
Section -AdditionalIcons
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateShortCut "$SMPROGRAMS\${PRODUCT_NAME}\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  CreateShortCut "$SMPROGRAMS\${PRODUCT_NAME}\Uninstall.lnk" "$INSTDIR\uninstall.exe"
SectionEnd

; å¸è½½éƒ¨åˆ†
Section "Uninstall"
  Delete "$INSTDIR\uninstall.exe"
  Delete "$INSTDIR\lanyiAI.exe"
  Delete "$INSTDIR\syspin.exe"

  Delete "$DESKTOP\${PRODUCT_NAME}.lnk"
  DeleteRegValue HKCU "Software\Microsoft\Windows\CurrentVersion\Run" "${PRODUCT_NAME}"

  Delete "$SMPROGRAMS\${PRODUCT_NAME}\Uninstall.lnk"
  Delete "$SMPROGRAMS\${PRODUCT_NAME}\Website.lnk"
  Delete "$SMPROGRAMS\${PRODUCT_NAME}\${PRODUCT_NAME}.lnk"

  RMDir /r "$SMPROGRAMS\${PRODUCT_NAME}"
  RMDir /r "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  SetAutoClose true
SectionEnd

; å¸è½½å‰æç¤º
Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "ä½ ç¡®å®è¦å®Œå…¨ç§»é™¤ $(^Name) ï¼ŒåŠå…¶æ‰€æœ‰ç»„ä»¶ï¼Ÿ" IDYES +2
  Abort
FunctionEnd

; å¸è½½æˆåŠŸæç¤º
Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) å·²æˆåŠŸä»ä½ çš„è®¡ç®—æœºä¸­ç§»é™¤ã€‚"
FunctionEnd
